import com.android.build.gradle.tasks.MergeSourceSetFolders
import com.nishtahir.CargoBuildTask

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.mozilla.rust-android-gradle.rust-android'
}

android {
    // Don't have the matching NDK for AGP installed: https://developer.android.com/studio/projects/install-ndk#default-ndk-per-agp
    ndkVersion '26.3.11579264'
    namespace 'rust.androidnativesurface'
    compileSdk 34

    defaultConfig {
        applicationId "rust.androidnativesurface"
        minSdk 28 // Should remain in sync with the ndk api-level-xx feature in Rust
        targetSdk 34
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

cargo {
    module = "../android_native_surface"
    libname = "android_native_surface"
    targets = ["arm64"]
}

project.afterEvaluate {
    tasks.withType(CargoBuildTask).forEach { buildTask ->
        tasks.withType(MergeSourceSetFolders).configureEach {
            it.inputs.dir(layout.buildDirectory.dir("rustJniLibs" + File.separatorChar + buildTask.toolchain.folder))
            it.dependsOn(buildTask)
        }
    }
}
