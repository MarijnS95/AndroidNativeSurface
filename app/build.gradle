plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.mozilla.rust-android-gradle.rust-android'
}

android {
    ndkVersion '25.2.9519653' // Don't have the matching NDK for AGP installed: https://developer.android.com/studio/projects/install-ndk#default-ndk-per-agp
    namespace 'rust.androidnativesurface'
    compileSdk 33

    defaultConfig {
        applicationId "rust.androidnativesurface"
        minSdk 29
        targetSdk 33
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
}

cargo {
    module = "../android_native_surface"
    libname = "android_native_surface"
    targets = ["arm64"]
}

project.afterEvaluate {
    tasks.withType(com.nishtahir.CargoBuildTask).forEach { buildTask ->
        tasks.withType(com.android.build.gradle.tasks.MergeSourceSetFolders).configureEach {
            it.inputs.dir(new File(new File(buildDir, "rustJniLibs"), buildTask.toolchain.folder))
            it.dependsOn(buildTask)
        }
    }
}
